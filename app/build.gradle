apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 21
    buildToolsVersion "21.0.1"

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE'
    }

    signingConfigs {
        release {
            storeFile file("../release.jks")
            keyAlias "release"
        }
        debug {
            storeFile file("../debug.keystore")
        }
    }

    defaultConfig {
        applicationId "de.devland.masterpassword"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 11
        versionName "1.2.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.+'

    // import, export
    compile(name:'aFileChooser-release', ext:'aar') // Apache v2.0
    compile 'com.google.code.gson:gson:2.3' // Apache v2.0

    // styling
    compile 'com.github.gabrielemariotti.cards:library:1.9.1' // Apache v2.0
    compile 'com.melnykov:floatingactionbutton:1.0.4' // MIT
    compile 'com.github.traex.rippleeffect:library:1.2.1' // MIT
    compile 'com.github.amlcurran.showcaseview:library:5.0.0' //  Apache v2.0
    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') { // Apache v2.0
        exclude group: 'com.google.android', module: 'support-v4'
    }

    // tools
    compile 'com.jakewharton:butterknife:5.1.2' // Apache v2.0
    compile 'com.squareup:otto:1.3.5' // Apache v2.0
    compile 'de.devland.esperandro:esperandro-api:2.1.0' // Apache v2.0
    compile 'de.devland.esperandro:esperandro-gson-addon:2.1.0' // Apache v2.0
    compile 'com.github.satyan:sugar:1.3' // MIT
    compile files('libs/lombok-api-1.14.0.jar') // MIT

    // annotation processors
    apt 'org.projectlombok:lombok:1.14.0' // MIT
    apt 'de.devland.esperandro:esperandro:2.1.0' // Apache v2.0

    // MasterPassword dependencies
    compile('com.lyndir.masterpassword:masterpassword-algorithm:398f7bdb') { // GPL v3
        exclude module: 'jsr305' // problems while dexing
    }
    compile 'org.slf4j:slf4j-android:1.7.7' // MIT
}
